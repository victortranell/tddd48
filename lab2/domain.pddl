;; Deliver system

(define (domain uav-domain)
  (:requirements :typing :strips :action-costs)
  (:types crate uav location person content object carrier num)
  (:constants
    food medicine - content
    depot - location
  )
  (:predicates
    (free ?u - uav)
    (at ?l - location ?o - object)
    (has ?o - object ?i - object)
    (needs ?p - person ?c - content)
    (next ?n0 ?n1 - num)
    (count ?carrier - carrier ?n - num)
  )
  (:functions 
    (total-cost) - number
    (fly-cost ?from ?to - location) - number
  )
  (:action drop
    :parameters(?u - uav ?l - location ?content - content ?crate - crate ?p - person)
    :precondition (and (has ?u ?crate) (at ?l ?u) (at ?l ?p) (needs ?p ?content) (has ?crate ?content))
    :effect (and (not (has ?u ?crate))
      (not (has ?crate ?content))
      (not (needs ?p ?content))
      (free ?u)
      (has ?p ?content)
      (at ?l ?crate)
      )
  )
  (:action flyto
    :parameters(?u - uav ?f - location ?t - location)
    :precondition(at ?f ?u)
    :effect(and (not (at ?f ?u))
           (at ?t ?u)
           (increase (total-cost) (fly-cost ?f ?t))
           )
  )
  (:action pickup 
    :parameters(?u - uav ?l - location ?c - crate)
    :precondition(and (free ?u)
                 (at ?l ?u)
                 (at ?l ?c)
                 )
    :effect(and (not (free ?u))
           (has ?u ?c)
           (not (at ?l ?c))
           )
  )
  (:action load-crate-on-carrier
    :parameters(?u - uav ?crate - crate ?carrier - carrier ?smallercount - num ?highercount - num ?l - location)
    :precondition(and (next ?smallercount ?highercount)
                 (has ?u ?crate)
                 (at ?l ?u)
                 (at ?l ?carrier)
                 (count ?carrier ?smallercount)
                 )
    :effect(and (count ?carrier ?highercount)
           (not (count ?carrier ?smallercount))
           (not (has ?u ?crate))
           (has ?carrier ?crate)
           (free ?u)
           )
  )
  (:action unload-crate-from-carrier
    :parameters(?u - uav ?crate - crate ?carrier - carrier ?smallercount - num ?highercount - num ?l - location)
    :precondition(and
                 (count ?carrier ?highercount)
                 (next ?smallercount ?highercount)
                 (has ?carrier ?crate)
                 (at ?l ?u)
                 (at ?l ?carrier)
                 (free ?u))
    :effect(and
           (not (free ?u))
           (not (has ?carrier ?crate))
           (count ?carrier ?smallercount)
           (not (count ?carrier ?highercount))
           (has ?u ?crate)
           (at ?l ?crate)
           )
  )
  (:action move-carrier
    :parameters(?uav - uav ?carrier - carrier ?from - location ?to - location)
    :precondition (and (at ?from ?carrier) (at ?from ?uav) (free ?uav))
    :effect(and (at ?to ?carrier)
            (not (at ?from ?carrier))
            (not (at ?from ?uav))
            (at ?to ?uav)
            (increase (total-cost) (fly-cost ?from ?to))
            )
  )
)
